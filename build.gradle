buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.1.6"
    }
}

plugins {
    id "org.springframework.boot" version "3.4.3" apply false
    id "com.gorylenko.gradle-git-properties" version "2.5.0" apply false
    id "com.github.johnrengelman.shadow" version "8.1.1" apply false
    id "io.freefair.lombok" version "8.13" apply false
}

tasks.register('printVersion') {
    doLast {
        println project.version
    }
}

allprojects {
    repositories {
        mavenCentral()
    }

    group = 'be.yonicon.template-java17-spring6-gradle'
    version = System.getenv('CI_COMMIT_SHORT_SHA') != null ? System.getenv('CI_COMMIT_SHORT_SHA').substring(0, 7) : 'local'
}

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'

    sourceCompatibility = JavaVersion.VERSION_17
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from(components.java)
            }
        }
    }

    tasks.named("test") {
        useJUnitPlatform {
            excludeTags "slow-test"
        }
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    tasks.register("slowTest", Test) {
        useJUnitPlatform {
            includeTags "slow-test"
        }
        testLogging {
            events "passed", "skipped", "failed"
        }
        shouldRunAfter test
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.boot:spring-boot-dependencies:3.4.3'
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:2024.0.1"
        }
        dependencies {
            dependency 'io.micrometer:micrometer-tracing-bridge-otel:1.4.4'
            dependency 'net.logstash.logback:logstash-logback-encoder:8.0'
            dependency 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.5'
        }
    }

    dependencies {
        implementation 'org.slf4j:slf4j-api'
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation 'org.assertj:assertj-core'
    }
}